#version 330 core

in vec4 vertex_position_cameraspace;
in vec4 vertex_normal_cameraspace;
in vec4 light_position_cameraspace;
in vec2 vertex_UV;
in vec4 vertex_position_lightspace;

uniform sampler2D shadowMapSampler;
uniform sampler2D diffuseColorSampler;
uniform sampler2D detailSampler;
uniform sampler2D specularColorSampler;
uniform sampler2D snailColorSampler;
uniform sampler2D splatMapSampler;
uniform sampler2D bouncySampler;
uniform int useTexture = 1;


// light properties
struct Light {
    vec4 La;
    vec4 Ld;
    vec4 Ls;
    vec3 lightPosition_worldspace;
};
uniform Light light;


// materials
struct Material {
    vec4 Ka; 
    vec4 Kd;
    vec4 Ks;
    float Ns; 
};
uniform Material mtl;

out vec4 fragmentColor;


void phong(float visibility);
float ShadowCalculation(vec4 fragPositionLightspace,sampler2D shadowMap, vec4 normal, vec4 lightDir);


vec3 hash3( vec2 p ) {
    vec3 q = vec3( dot(p,vec2(127.1,311.7)), 
                   dot(p,vec2(269.5,183.3)), 
                   dot(p,vec2(419.2,371.9)) );
    return fract(sin(q)*43758.5453);
}

float valueNoise(vec2 uv) {
    vec2 i = floor(uv);
    vec2 f = fract(uv);
    // Smooth interpolation curve
    f = f * f * (3.0 - 2.0 * f);

    // Reuse your existing hash3 function
    float a = hash3(i).x;
    float b = hash3(i + vec2(1.0, 0.0)).x;
    float c = hash3(i + vec2(0.0, 1.0)).x;
    float d = hash3(i + vec2(1.0, 1.0)).x;

    return mix(mix(a, b, f.x), mix(c, d, f.x), f.y);
}
vec4 textureNoTile(sampler2D samp, in vec2 uv) {
    return texture(samp, uv); // Simple, standard mapping. No glitches.
}
void main() {   
    
    // Task 4.3
    vec4 N = normalize(vertex_normal_cameraspace);
    vec4 L = normalize(light_position_cameraspace - vertex_position_cameraspace);
    float visibility = 1.0f;
    visibility -= ShadowCalculation(vertex_position_lightspace,shadowMapSampler, N, L) * 0.7f;

    phong(visibility);
}



float ShadowCalculation(vec4 fragPositionLightspace,sampler2D shadowMap, vec4 normal, vec4 lightDir) {

    vec3 projCoords = fragPositionLightspace.xyz / fragPositionLightspace.w;

    projCoords = (projCoords + 1)* 0.5 ; 

    float closestDepth = texture(shadowMap,projCoords.xy).r;

    float currentDepth = projCoords.z;
    float bias = max(0.05 * (1.0 - dot(normal, lightDir)), 0.005);
    float shadow = 0.0;
    vec2 depthMap_dimensions = textureSize(shadowMap, 0);
    vec2 texelSize = 1.0 / depthMap_dimensions;
    for(int x = -1; x <= 1; x++ ){
        for(int y = -1; y <= 1; y++ ){
            float pcfDepth = texture(shadowMap, projCoords.xy + vec2(x, y) * texelSize).r;
            shadow += currentDepth - bias > pcfDepth ? 1.0 : 0.0;
        }
    }
    shadow /= 9.0;
    
    // Inside ShadowCalculation, before returning shadow
    if(projCoords.z > 1.0 || projCoords.z < 0.0||projCoords.x > 1.0 || projCoords.x < 0.0 || projCoords.y > 1.0 || projCoords.y < 0.0) shadow = 0.0;
    


    return shadow;

}


void phong(float visibility) {
   
    vec4 _Ks = mtl.Ks;
    vec4 _Kd = mtl.Kd;
    vec4 _Ka = mtl.Ka;
    float _Ns = mtl.Ns;

    // use texture for materials
    if (useTexture == 1) {
        _Ks = vec4(texture(specularColorSampler, vertex_UV).rgb, 1.0);

        // 1. Sample textures
        vec2 tiledUV = vertex_UV * 20.0;
        vec4 grassColor = textureNoTile(diffuseColorSampler, tiledUV);
        vec4 rockColor  = textureNoTile(detailSampler, tiledUV);

        vec4 bouncyColor = textureNoTile(bouncySampler, tiledUV); // Texture 3

        // Read the Splat Map (RGB)
        vec3 splat = texture(splatMapSampler, vertex_UV).rgb;
    
        // splat.r = Rock Amount
        // splat.g = Bouncy Amount

        // 1. Mix Grass and Rock first
        float rockFactor = smoothstep(0.4, 0.6, splat.r);
        vec4 finalColor = mix(grassColor, rockColor, rockFactor);

        // 2. Now Mix the Result with Bouncy based on Green channel
        float bouncyFactor = smoothstep(0.4, 0.6, splat.g);
    
        _Kd = mix(finalColor, bouncyColor, bouncyFactor);
        
        _Ka = vec4(0.05 * _Kd.rgb, _Kd.a);
    }
    
    // model ambient intensity (Ia)
    vec4 Ia = (light.La )* _Ka;

    // model diffuse intensity (Id)
    vec4 N = normalize(vertex_normal_cameraspace); 
    vec4 L = normalize(light_position_cameraspace - vertex_position_cameraspace);
    float cosTheta = clamp(dot(N, L), 0, 1);
    vec4 Id = (light.Ld ) * _Kd * cosTheta;

    // model specular intensity (Is)
    vec4 R = reflect(-L, N);
    vec4 E = normalize(vec4(0, 0, 0, 1) - vertex_position_cameraspace);
    float cosAlpha = clamp(dot(E, R), 0, 1);
    float specular_factor = pow(cosAlpha, _Ns);
    vec4 Is = (light.Ls) * _Ks * specular_factor;

    
    fragmentColor = vec4(
        Ia + 
        (Id + Is) * visibility);
}